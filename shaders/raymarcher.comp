#version 450

layout (binding = 0) uniform UniformBufferObject {
    vec3 position;
    vec3 viewDirection;
    float FOV;
} ubo;

layout (binding = 1, rgba8) uniform writeonly image2D outputImage;

struct GPUOctreeNode {
    uint data;
};

struct OctreeNode {
    uint childrenIndex;
    int value;
    int flags;
    int a, b, c, d, e, f;
    vec4 color;
};

struct ReturnedOctreeNode {
    int value;
    int minX, maxX, minY, maxY, minZ, maxZ;
    vec4 color;
};

layout(std140, binding = 2) readonly buffer Octree{
    OctreeNode nodes[20000];
} octree;

layout (local_size_x = 4, local_size_y = 4, local_size_z = 1) in;

struct Ray {
    vec3 origin;
    vec3 direction;
};

int WORLD_SIZE = 64;
ReturnedOctreeNode emptyNode = ReturnedOctreeNode(-1, 0, 0, 0, 0, 0, 0, vec4(0.0f));
ReturnedOctreeNode getLowestOctreeNode(in vec3 position) {
    if (position.x < 0 || position.x >= WORLD_SIZE || position.y < 0 || position.y >= WORLD_SIZE || position.z < 0 || position.z >= WORLD_SIZE)
        return emptyNode;

    OctreeNode node = octree.nodes[0];

    float halfSize = WORLD_SIZE/2.0f;
    int minX = 0, minY = 0, minZ = 0;
    while (node.flags == 0 && node.childrenIndex != -1 && halfSize*2 > 1) {
        vec3 difference = vec3((position.x-minX)/halfSize, (position.y-minY)/halfSize, (position.z-minZ)/halfSize);
        int index = int(floor(difference.x) + floor(difference.y)*2 + floor(difference.z)*4);
        
        minX += int(floor(difference.x)*halfSize);
        minY += int(floor(difference.y)*halfSize);
        minZ += int(floor(difference.z)*halfSize);
        halfSize /= 2.0f;

        node = octree.nodes[node.childrenIndex+index];
    }

    return ReturnedOctreeNode(node.value, minX, minX+int(halfSize*2.0f), minY, minY+int(halfSize*2.0f), minZ, minZ+int(halfSize*2.0f), node.color);
}

int MAX_STEPS = 1000;
float MAX_DISTANCE = 100.0f;
vec3 marchRay(in Ray ray) {
    vec3 returnColor = vec3(0.0f);
    float opacityUsed = 0.0f;
    vec3 position = ray.origin;
    vec3 direction = ray.direction;
    float totalDistance = 0.0f;

    int MAX_STEPS = 1000, i = 0;
    while (i++ < MAX_STEPS && totalDistance < MAX_DISTANCE && opacityUsed < 1.0f) {
        if (position.z <= 0) {
            if (int(floor(position.x-direction.x*position.z/direction.z) + floor(position.y-direction.y*position.z/direction.z)) % 2 == 0) {
                returnColor += vec3(1.0f)*(1.0f-opacityUsed);
                break;
            } else {
                returnColor += vec3(0.0f)*(1.0f-opacityUsed);
                break;
            }
        }

        ReturnedOctreeNode lowestNode = getLowestOctreeNode(position);

        if (lowestNode.value > 0) {
            returnColor += lowestNode.color.rgb*(1.0f-opacityUsed)*lowestNode.color.a;
            opacityUsed += lowestNode.color.a*(1.0f-opacityUsed)*1.1;
        }

        int size = lowestNode.maxX-lowestNode.minX;
        float lowestDistance = 100.0f;
        if (position.x < 0 || position.x >= WORLD_SIZE || position.y < 0 || position.y >= WORLD_SIZE || position.z < 0 || position.z >= WORLD_SIZE) {
            lowestDistance = 1.0f;   
        }
        else {
            if (direction.x > 0) {
                lowestDistance = (size - (position.x - lowestNode.minX)) / abs(direction.x);
            } else if (direction.x < 0) {
                lowestDistance = (size - (lowestNode.maxX - position.x)) / abs(direction.x);
            }
            if (direction.y > 0) {
                lowestDistance = min(lowestDistance, (size - (position.y - lowestNode.minY)) / abs(direction.y));
            } else if (direction.y < 0) {
                lowestDistance = min(lowestDistance, (size - (lowestNode.maxY - position.y)) / abs(direction.y));
            }
            if (direction.z > 0) {
                lowestDistance = min(lowestDistance, (size - (position.z - lowestNode.minZ)) / abs(direction.z));
            } else if (direction.z < 0) {
                lowestDistance = min(lowestDistance, (size - (lowestNode.maxZ - position.z)) / abs(direction.z));
            }
        }
        lowestDistance = lowestDistance+0.001f;
        position += direction*lowestDistance;
        totalDistance += lowestDistance;
    }

    return returnColor;
}

float CANVAS_DISTANCE = 1;
vec3 WORLD_UP = vec3(0.0f, 0.0f, 1.0f);
Ray createRay() {
    vec2 uv = vec2(gl_GlobalInvocationID.xy) / vec2(imageSize(outputImage))*2-1;
	vec3 uvRight = normalize(cross(ubo.viewDirection, WORLD_UP));
	vec3 uvDown = normalize(cross(ubo.viewDirection, uvRight));
	vec3 toCanvas = ubo.viewDirection * CANVAS_DISTANCE;
	vec2 canvasSize = imageSize(outputImage);
	vec3 rayPosition = ubo.position + toCanvas + (uvRight * uv.x) + (uvDown * uv.y);
	vec3 rayDirection = normalize(rayPosition - ubo.position);

    return Ray(rayPosition, rayDirection);
}

void main() {
    Ray ray = createRay();

    imageStore(outputImage, ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y), vec4(marchRay(ray), 1.0f));
    //imageStore(outputImage, ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y), vec4((gl_GlobalInvocationID.x/16)/15.0f, (gl_GlobalInvocationID.y/16)/15.0f, (gl_GlobalInvocationID.x % 16)/15.0f, 1.0f));
}
